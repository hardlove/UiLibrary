apply plugin: 'com.android.library'
apply from: "../jitpack.gradle"

android {
    compileSdkVersion 32
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 10
        versionName "1.0.0"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    api 'androidx.appcompat:appcompat:1.4.1'
    api 'com.google.android.material:material:1.4.0'

    // CameraX core library using the camera2 implementation
    def camerax_version = "1.2.0-beta01"
    // The following line is optional, as the core library is included indirectly by camera-camera2
    api("androidx.camera:camera-core:${camerax_version}")
    api("androidx.camera:camera-camera2:${camerax_version}")
    // If you want to additionally use the CameraX Lifecycle library
    api("androidx.camera:camera-lifecycle:${camerax_version}")
    // If you want to additionally use the CameraX VideoCapture library
    api("androidx.camera:camera-video:${camerax_version}")
    // If you want to additionally use the CameraX View class
    api("androidx.camera:camera-view:${camerax_version}")
    // If you want to additionally add CameraX ML Kit Vision Integration
    api("androidx.camera:camera-mlkit-vision:${camerax_version}")
    // If you want to additionally use the CameraX Extensions library
    api("androidx.camera:camera-extensions:${camerax_version}")


}




apply plugin: 'maven-publish'
task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

publishing {
    publications {
        Production(MavenPublication) {
            // 使用方引用 implementation "com.github.hardlove.UiLibrary:CameraX:2.4.1"
            groupId = "com.github.hardlove.UiLibrary"
            artifactId = "CameraX"
            version = "2.4.41"
            afterEvaluate {
                artifact(tasks.getByName("bundleReleaseAar"))
            }
            artifact generateSourcesJar


            //添加三方依赖库
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each {
                    //遍历依赖库
                    if (it.group != null && (it.name != null && "unspecified" != it.name) && it.version != null) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                        dependencyNode.appendNode('scope', 'implementation')
                    }

                }
            }
        }
    }
    repositories {
        maven {
//            url = "$rootDir/repos"
//            url = "${rootDir.parent}/repos"
            url = "D:\\Work\\repos"
//            url = "../../../repos"
        }
        mavenLocal()
    }
}